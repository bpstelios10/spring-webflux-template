buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:9.4.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'scala'

repositories {
    mavenCentral()
}

ext {
    simulation = project.properties.getOrDefault("simulation",
            "framework.templates.springbootwebflux.nft.simulations.ServiceSimulation")
    testDuration = project.properties.getOrDefault('testDuration', "60") as int
    targetEnvironment = project.properties.getOrDefault("targetEnvironment", "local")
    testType = project.properties.getOrDefault("testType", "defaultSimulation")
    testTimeout = testDuration + 1800
}

dependencies {
    implementation 'io.gatling.highcharts:gatling-charts-highcharts:3.9.5'
    implementation 'commons-codec:commons-codec:1.16.0'
}

project.version = '0.12.0'
apply from: "repo-tasks.gradle"
apply from: "helm-tasks.gradle"

shadowJar {
    archiveFileName = 'service-nft.jar'
}
jar.enabled = false
jar.dependsOn 'shadowJar'

task gatlingRun(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'io.gatling.app.Gatling'

    jvmArgs("-DtestDuration=${testDuration}",
            "-DtargetEnvironment=${targetEnvironment}",
            "-DtestType=${testType}",
            '-Dlogger=console-json',
            '-XX:+UseParallelGC',
            '-XX:MaxGCPauseMillis=30',
            '-XX:G1HeapRegionSize=2m',
            '-XX:InitiatingHeapOccupancyPercent=50',
            '-XX:+ParallelRefProcEnabled',
            '-XX:+PerfDisableSharedMem',
            '-XX:+OptimizeStringConcat',
            '-XX:+HeapDumpOnOutOfMemoryError',
            '-Djava.net.preferIPv4Stack=true',
            '-Djava.net.preferIPv6Addresses=false'
    )

    args(['-bf', 'build/classes/scala/main', '-rf', 'build/results', '-s', "${simulation}"])
}
gatlingRun.dependsOn 'build'
