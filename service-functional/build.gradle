repositories {
    mavenCentral()
}

dependencies {
    implementation project(":service") //for this to work in spring boot, we need to enable jar in service module
    implementation(project(":service-mocks")) {
        exclude group: "javax.servlet", module: "javax.servlet-api"
    }
    implementation project(":service").sourceSets.main.output
    implementation project(":service-mocks").sourceSets.main.output

    implementation "io.cucumber:cucumber-jvm:4.2.6"
    implementation "io.cucumber:cucumber-spring:4.2.6"
    implementation "io.cucumber:cucumber-java8:4.2.6"

    implementation "org.assertj:assertj-core:3.16.1"
    implementation "org.apache.httpcomponents:httpclient:4.5.3"
    implementation 'org.apache.commons:commons-collections4:4.1'
    // Fixes running tests in IntelliJ - don't set as runtime dependency, breaks remote functional tests
    implementation "org.springframework:spring-tx:${versions.spring}"
    implementation "com.github.tomakehurst:wiremock-jre8:2.23.2"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
    testImplementation(
            "org.junit.jupiter:junit-jupiter-params:5.5.2",
            "org.mockito:mockito-junit-jupiter:3.0.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    test.useJUnitPlatform()
}

jar {
    enabled = true
}

task localFunctional(type: JavaExec) {
    group = 'Verification'
    main = 'cucumber.api.cli.Main'
    classpath = project.sourceSets.test.runtimeClasspath
    args = ['--strict',
            "--tags", "not @wip and not @open-circuit",
            "--plugin", "pretty",
            "--plugin", "html:build/cucumber-report",
            "--plugin", "junit:build/junit-test-report.xml",
            "--plugin", "json:build/cucumber-report.json",
            "--glue", "framework.templates.springbootwebflux.functional.steps",
            "src/main/resources/features/"
    ]
    jvmArgs = ["-XX:MaxMetaspaceSize=128m", "-Xmx256m", "-Xms256m", "-Xss256k"]
    dependsOn = ['testClasses']
}

task openCircuitFunctional(type: JavaExec) {
    group = 'Verification'
    main = 'cucumber.api.cli.Main'
    classpath = project.sourceSets.test.runtimeClasspath
    args = ['--strict',
            '--tags', '@open-circuit and not @wip',
            "--plugin", "pretty",
            "--plugin", "html:build/cucumber-report",
            "--plugin", "junit:build/junit-test-report.xml",
            "--plugin", "json:build/cucumber-report.json",
            "--glue", "framework.templates.springbootwebflux.functional.steps",
            "src/main/resources/features/"
    ]
    jvmArgs = ["-XX:MaxMetaspaceSize=128m", "-Xmx256m", "-Xms256m", "-Xss256k"]
    dependsOn = ['testClasses']
    mustRunAfter = [localFunctional]
}

check.dependsOn (localFunctional, openCircuitFunctional)
