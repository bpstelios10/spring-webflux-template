repositories {
    mavenCentral()
}

dependencies {
    implementation project(":service") //for this to work in spring boot, we need to enable jar in service module
    implementation(project(":service-mocks")) {
        exclude group: "javax.servlet", module: "javax.servlet-api"
    }
    implementation project(":service").sourceSets.main.output
    implementation project(":service-mocks").sourceSets.main.output

    implementation 'io.cucumber:cucumber-jvm:7.15.0'
    implementation 'io.cucumber:cucumber-spring:7.15.0'
    implementation 'io.cucumber:cucumber-java8:7.15.0'
    implementation 'io.cucumber:cucumber-junit:7.15.0'

    implementation 'org.assertj:assertj-core:3.24.2'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.commons:commons-collections4:4.4'
    // Fixes running tests in IntelliJ - don't set as runtime dependency, breaks remote functional tests
    implementation "org.springframework:spring-tx:${versions.spring}"
    implementation "org.springframework.boot:spring-boot-starter-test:2.3.12.RELEASE"
    implementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    test.useJUnitPlatform()
}

jar.enabled = false

task localFunctional(type: JavaExec) {
    group = 'Verification'
    mainClass = 'io.cucumber.core.cli.Main'
    classpath = project.sourceSets.test.runtimeClasspath
    args = [
            "--tags", "not @wip and not @open-circuit",
            "--plugin", "pretty",
            "--plugin", "html:build/cucumber-report",
            "--plugin", "junit:build/junit-test-report.xml",
            "--plugin", "json:build/cucumber-report.json",
            "--glue", "framework.templates.springbootwebflux.functional.steps",
            "src/main/resources/features/"
    ]
    jvmArgs = ["-XX:MaxMetaspaceSize=128m", "-Xmx256m", "-Xms256m", "-Xss256k"]
    dependsOn = ['testClasses']
}

task openCircuitFunctional(type: JavaExec) {
    group = 'Verification'
    mainClass = 'io.cucumber.core.cli.Main'
    classpath = project.sourceSets.test.runtimeClasspath
    args = [
            '--tags', '@open-circuit and not @wip',
            "--plugin", "pretty",
            "--plugin", "html:build/cucumber-report",
            "--plugin", "junit:build/junit-test-report.xml",
            "--plugin", "json:build/cucumber-report.json",
            "--glue", "framework.templates.springbootwebflux.functional.steps",
            "src/main/resources/features/"
    ]
    jvmArgs = ["-XX:MaxMetaspaceSize=128m", "-Xmx256m", "-Xms256m", "-Xss256k"]
    dependsOn = ['testClasses']
    mustRunAfter = [localFunctional]
}

check.dependsOn (localFunctional, openCircuitFunctional)
